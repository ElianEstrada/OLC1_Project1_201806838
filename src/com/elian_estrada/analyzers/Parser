/*
 * Ejemplo Parser
 * Elian Estrada
 * Febrero 2021
 */


package com.elian_estrada.analyzers;

import com.elian_estrada.classes.*;
import java.util.*;
import java_cup.runtime.*;

action code 
{:

    public Hashtable<String, SymbolTable> symbolTable = new Hashtable<String, SymbolTable>();
    public Hashtable<Integer, ArrayList<NodeTree>> leaves = new Hashtable<Integer, ArrayList<NodeTree>>();
    public ArrayList<String> terminalSymbol = new ArrayList<String>();
    //public Tree tree;
    public int count = 0;
    public int countAfn = 0;
:};

parser code
{: 

    int countErrors = 0;
    public boolean errors = false;
    public ArrayList<SintacticError> sintacticError = new ArrayList<SintacticError>();

    /*Metodo para errores sintacticos*/
    public void syntax_error(Symbol s){
        errors = true;
        System.out.println("Error sintactico en la linea: " + (s.left) + 
        " columna: " + s.right + ". No se esperaba este componente: " + s.value + 
        ".");
        countErrors++;
        sintacticError.add(new SintacticError(countErrors, "No se esperaba: " + s.value + ".", s.left, s.right));

    }

    /*Metodo para errores fatales*/
    
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception {
        errors = true;
        System.out.println("Error sintactico irrecuperable en la linea: " + 
        (s.left) + " columna: " + s.right + ". Componente: " + s.value + 
        " no reconocido.");
        countErrors++;
        sintacticError.add(new SintacticError(countErrors, "Fatal Error, no se esperaba: " + s.value + ".", s.left, s.right));
        System.out.println(sintacticError);
    }

    public String drawing (ArrayList<SintacticError> errorlist){
        
        String tables = "<div class=\"collapsible-body\">\n<table class =\"striped\">\n"
                + "<thead>\n<tr>\n<th> No. </th>\n<th> Description </th>\n<th> Line </th>\n<th> Column </th>\n</thead>\n<tbody>\n";
        for (int i = 0; i < errorlist.size(); i++){
            tables += "<tr>\n<td> " + errorlist.get(i).getId() + " </td>\n"
                    + "<td> " + errorlist.get(i).getDescription() + "</td>\n"
                    + "<td> " + errorlist.get(i).getLine() + "</td>\n"
                    + "<td> " + errorlist.get(i).getColumn() + "</td>\n</tr>\n";
        }
        
        tables += "</tbody>\n</table>\n</div>\n";
        
        return tables;
    }


:};

terminal String PTCOMA, COMA,DOSPUN,PARIZQ,PARDER, LLAVIZQ, LLAVDER, ASIG, RANG, PORCENT;
terminal String KLEAN,POS,CONCAT,DISYN, BOOL;
terminal String ENTERO;
terminal String DIGIT;
terminal String DECIMAL;
terminal String UMENOS;
terminal String CONJ;
terminal String CADENA_TEXTO;
terminal String IDENTIFICADOR;
terminal String LETTER;
terminal String NLINE, COMILLA, COMDOBLE;
terminal String SIMBOLS;

non terminal Init;
non terminal Statement;
non terminal Validation;
non terminal StatementInstrucctions;
non terminal String Set;
non terminal String SetType;
non terminal String SetTypeP;
non terminal String Signs;
non terminal String ListSet;
non terminal Tree RegularPhrase;
non terminal NodeTree Expression;
non terminal ValidationInstrucctions;

precedence left CONCAT, DISYN;
precedence left KLEAN, POS, BOOL;

start with Init;

Init ::= LLAVIZQ Statement PORCENT PORCENT Validation LLAVDER                   {:if(errors){System.out.println(sintacticError);}:}
        |error
;                  

Statement ::= 
     StatementInstrucctions Statement
    |StatementInstrucctions
    |error PTCOMA Statement
;

StatementInstrucctions ::= 
     Set:a                                                                      {:if(!errors) { System.out.println(symbolTable); }:}
    |RegularPhrase:a                                                            {:
                                                                                    if(!errors) {count = 0; a.followTable(leaves); a.transitionsTable(leaves, symbolTable); 
                                                                                        System.out.println(a.chart()); leaves.clear(); terminalSymbol.clear(); }
                                                                                :}  
;

Set ::= 
     CONJ:a DOSPUN IDENTIFICADOR:b ASIG SetType:d PTCOMA                        {: 
                                                                                    if(!errors) { symbolTable.put(b, new SymbolTable(b, d)); RESULT = a; }
                                                                                :}
    /*|error PTCOMA*/
;

SetType ::= 
     Signs:a SetTypeP:b                                                         {:RESULT = a.toString() + b.toString();:} 
;

Signs ::= 
     SIMBOLS:a                                                                  {:RESULT = a;:}
    |LETTER:a                                                                   {:RESULT = a;:}
    |DIGIT:a                                                                    {:RESULT = a;:}
;

SetTypeP ::= 
     RANG Signs:a                                                               {:RESULT = "~" + a;:}
    |ListSet:a                                                                  {:RESULT = a;:}
;

ListSet ::= 
     COMA Signs:a ListSet:b                                                     {:RESULT = ", " + a + b;:}
    |/*Eps*/                                                                    {:RESULT = "";:}        
;

RegularPhrase ::= 
     IDENTIFICADOR:b ASIG Expression:a PTCOMA                                   {: 
                                                                                    if(!errors){
                                                                                        count++;
                                                                                        RESULT = new Tree(new NodeTree(".", a, new NodeTree("Â¿", count, leaves), leaves), b, terminalSymbol);
                                                                                    }
                                                                                :}
    /*|error RegularPhrase                                                        {::}*/
;

Expression ::=
     CONCAT Expression:a Expression:b                                           {:if(!errors){RESULT = new NodeTree(".", a, b, leaves);}:}
    |DISYN Expression:a Expression:b                                            {:if(!errors){RESULT = new NodeTree("|", a, b, leaves);}:}
    |POS Expression:a                                                           {:if(!errors){RESULT = new NodeTree("+", a, null, leaves);}:}
    |KLEAN Expression:a                                                         {:if(!errors){RESULT = new NodeTree("*", a, null, leaves);}:}
    |BOOL Expression:a                                                          {:if(!errors){RESULT = new NodeTree("?", a, null, leaves);}:}
    |LLAVIZQ IDENTIFICADOR:a LLAVDER                                            {:if(!errors){count++; if(!terminalSymbol.contains(a)) {terminalSymbol.add(a);} RESULT = new NodeTree(a, count, leaves);}:}
    |CADENA_TEXTO:a                                                             {:if(!errors){count++; if(!terminalSymbol.contains(a)) {terminalSymbol.add(a);} RESULT = new NodeTree(a, count, leaves);}:}
    |NLINE:a                                                                    {:if(!errors){count++; if(!terminalSymbol.contains(a)) {terminalSymbol.add(a);} RESULT = new NodeTree(a, count, leaves);}:}
    |COMILLA:a                                                                  {:if(!errors){count++; if(!terminalSymbol.contains(a)) {terminalSymbol.add(a);} RESULT = new NodeTree(a, count, leaves);}:}
    |COMDOBLE:a                                                                 {:if(!errors){count++; if(!terminalSymbol.contains(a)) {terminalSymbol.add(a);} RESULT = new NodeTree(a, count, leaves);}:}
;


Validation ::= 
     ValidationInstrucctions Validation
    |ValidationInstrucctions
    /*|error LLAVDER*/
    |error Validation
    |error LLAVDER
;

ValidationInstrucctions ::= 
     IDENTIFICADOR DOSPUN CADENA_TEXTO PTCOMA
;

/*
instruccion ::=
    CORIZQ expresion:a CORDER PTCOMA {: System.out.println("El valor de la expresion es: " + a);:}
;

expresion ::= 
    ENTERO:a POS ENTERO:b                                       {:RESULT= a+b;:}
   |DECIMAL:a                                       {:RESULT= a;:}
   |PARIZQ expresion:a PARDER                       {:RESULT= a;:}
   |CADENA_TEXTO:a                                  {:RESULT= a; System.out.println(a);:}
   |SIMBOLS:a    RANG COMDOBLE                      {:RESULT= a; System.out.println(a);:}
;*/